buildscript {
	// The version of Kotlin I am using, soon moving to 1.1
	ext.kotlin_version = '1.0.6'

	repositories {
		mavenCentral()
		maven {
			name 'JFrog OSS snapshot repo'
			url 'https://oss.jfrog.org/oss-snapshot-local/'
		}
		jcenter()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

apply plugin: 'kotlin'

apply plugin: 'antlr'
// I use IntelliJ IDEA and this plugin permit to generate project files for that IDE
apply plugin: 'idea'
repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	antlr "org.antlr:antlr4:4.5.1"
	compile "org.antlr:antlr4-runtime:4.5.1"
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
	testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
	testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
	testCompile 'junit:junit:4.12'
}
// This is the task to generate the lexer using ANTLR
generateGrammarSource {
	maxHeapSize = "64m"
	arguments += ['-package', 'me.tomassetti.minicalc']
	outputDirectory = new File("generated-src/antlr/main/me/tomassetti/minicalc".toString())
}
// We want to compile the generated lexer AFTER having generate it
compileJava.dependsOn generateGrammarSource
	sourceSets {
	generated {
		java.srcDir 'generated-src/antlr/main/'
	}
}

compileJava.source sourceSets.generated.java, sourceSets.main.java
compileKotlin.source sourceSets.generated.java, sourceSets.main.java, sourceSets.main.kotlin
// When we run ./gradlew clean we want to remove the generated code
clean{
	delete "generated-src"
}

idea {
	module {
		sourceDirs += file("generated-src/antlr/main")
	}
}
